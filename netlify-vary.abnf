Netlify-Vary = ( ( header-directives / query-directives / cookie-directives / language-directives / country-directives ) *( OWS "," OWS [ header-directives / query-directives / cookie-directives / language-directives / country-directives ] ) )

header-directives = %s"header=" ( OWS field-name ) *( OWS "|" OWS ( field-name ) )

cookie-directives = %s"cookie=" ( OWS cookie-name ) *( OWS "|" OWS ( cookie-name ) )

language-directives = %s"language=" ( OWS combo-language-range ) *( OWS "|" OWS ( combo-language-range ) )

country-directives = %s"country=" ( OWS combo-iso-3166-1-code ) *( OWS "|" OWS ( combo-iso-3166-1-code ) )

query-directives = %s"query" [ "=" OWS ( query ) *( OWS "|" OWS ( query ) ) ]

combo-language-range = language-range *( OWS "+" OWS ( language-range) )

combo-iso-3166-1-code = ( iso-3166-1-code *( OWS "+" OWS ( iso-3166-1-code) ) )

cookie-name = token

query = 1*( pchar )

pchar = unreserved / pct-encoded / "*"

unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~" / "+"

pct-encoded = "%" HEXDIG HEXDIG

field-name = token

OWS = *( SP / HTAB )

tchar = "!" / "#" / "$" / "%" / "&" / "'" / "*" / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~" / DIGIT / ALPHA

token = 1*tchar

language-range   = (1*8ALPHA *("-" 1*8alphanum)) / "*"

alphanum = ALPHA / DIGIT

iso-3166-1-code = ALPHA ALPHA

ALPHA = %x41-5A / %x61-7A

DIGIT = %x30-39

SP = %x20

HTAB = %x09

HEXDIG = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
