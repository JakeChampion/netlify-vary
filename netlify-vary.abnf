Netlify-Vary = ( ( header-directives / query-directives / cookie-directives / language-directives / country-directives ) *( OWS "," OWS [ header-directives / query-directives / cookie-directives / language-directives / country-directives ] ) )

header-directives = %s"header=" ( field-name ) *( OWS "|" OWS ( field-name ) )

cookie-directives = %s"cookie=" ( cookie-name ) *( OWS "|" OWS ( cookie-name ) )

language-directives = %s"language=" ( combo-language-range ) *( OWS "|" OWS ( combo-language-range ) )

country-directives = %s"country=" ( combo-iso-3166-1-code ) *( OWS "|" OWS ( combo-iso-3166-1-code ) )

query-directives = %s"query" [ "=" ( query ) *( OWS "|" OWS ( query ) ) ]

combo-language-range = language-range *( OWS "+" OWS ( language-range) )

combo-iso-3166-1-code = ( iso-3166-1-code *( OWS "+" OWS ( iso-3166-1-code) ) )

cookie-name = token

query = 1*( pchar )

pchar = unreserved / pct-encoded / "*"

unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~" / "+"

pct-encoded = "%" HEXDIG HEXDIG

field-name = token

OWS = *( SP / HTAB )

tchar = "!" / "#" / "$" / "%" / "&" / "'" / "*" / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~" / DIGIT / ALPHA

token = 1*tchar

language-range   = (1*8ALPHA *("-" 1*8alphanum)) / "*"

alphanum = ALPHA / DIGIT

iso-3166-1-code = "AD" / "AE" / "AF" / "AG" / "AI" / "AL" / "AM" / "AO" / "AQ" / "AR" / "AS" / "AT" / "AU" / "AW" / "AX" / "AZ" / "BA" / "BB" / "BD" / "BE" / "BF" / "BG" / "BH" / "BI" / "BJ" / "BL" / "BM" / "BN" / "BO" / "BQ" / "BR" / "BS" / "BT" / "BV" / "BW" / "BY" / "BZ" / "CA" / "CC" / "CD" / "CF" / "CG" / "CH" / "CI" / "CK" / "CL" / "CM" / "CN" / "CO" / "CR" / "CU" / "CV" / "CW" / "CX" / "CY" / "CZ" / "DE" / "DJ" / "DK" / "DM" / "DO" / "DZ" / "EC" / "EE" / "EG" / "EH" / "ER" / "ES" / "ET" / "FI" / "FJ" / "FK" / "FM" / "FO" / "FR" / "GA" / "GB" / "UK" / "GD" / "GE" / "GF" / "GG" / "GH" / "GI" / "GL" / "GM" / "GN" / "GP" / "GQ" / "GR" / "GS" / "GT" / "GU" / "GW" / "GY" / "HK" / "HM" / "HN" / "HR" / "HT" / "HU" / "ID" / "IE" / "IL" / "IM" / "IN" / "IO" / "IQ" / "IR" / "IS" / "IT" / "JE" / "JM" / "JO" / "JP" / "KE" / "KG" / "KH" / "KI" / "KM" / "KN" / "KP" / "KR" / "KW" / "KY" / "KZ" / "LA" / "LB" / "LC" / "LI" / "LK" / "LR" / "LS" / "LT" / "LU" / "LV" / "LY" / "MA" / "MC" / "MD" / "ME" / "MF" / "SX" / "MG" / "MH" / "MK" / "ML" / "MM" / "MN" / "MO" / "MP" / "MQ" / "MR" / "MS" / "MT" / "MU" / "MV" / "MW" / "MX" / "MY" / "MZ" / "NA" / "NC" / "NE" / "NF" / "NG" / "NI" / "NL" / "NO" / "NP" / "NR" / "NU" / "NZ" / "OM" / "PA" / "PE" / "PF" / "PG" / "PH" / "PK" / "PL" / "PM" / "PN" / "PR" / "PS" / "PT" / "PW" / "PY" / "QA" / "RE" / "RO" / "RS" / "RU" / "RW" / "SA" / "SB" / "SC" / "SD" / "SE" / "SG" / "SH" / "SI" / "SJ" / "SK" / "SL" / "SM" / "SN" / "SO" / "SR" / "SS" / "ST" / "SV" / "SX" / "MF" / "SY" / "SZ" / "TC" / "TD" / "TF" / "TG" / "TH" / "TJ" / "TK" / "TL" / "TM" / "TN" / "TO" / "TR" / "TT" / "TV" / "TW" / "TZ" / "UA" / "UG" / "UM" / "US" / "UY" / "UZ" / "VA" / "VC" / "VE" / "VG" / "VI" / "VN" / "VU" / "WF" / "WS" / "YE" / "YT" / "ZA" / "ZM" / "ZW"

ALPHA = %x41-5A / %x61-7A

DIGIT = %x30-39

SP = %x20

HTAB = %x09

HEXDIG = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
